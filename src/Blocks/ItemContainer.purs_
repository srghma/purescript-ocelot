module Ocelot.Block.ItemContainer where

import Prelude

import TailwindClasses as TailwindClasses
import DOM.HTML.Indexed (HTMLbutton, HTMLdiv)
import Data.Array ((:))
import Data.Either (Either(..))
import Data.FunctorWithIndex (mapWithIndex)
import Data.Fuzzy (Fuzzy(..))
import Data.Maybe (Maybe(..), fromMaybe)
import Foreign.Object (lookup)
import Halogen.HTML as HH
import Halogen.HTML.Properties as HP
import Ocelot.Block.Icon as Icon
import Ocelot.HTML.Properties (IProp, css, (<&>))
import Select as Select
import Select.Setters as Setters

menuClasses :: Array HH.ClassName
menuClasses =
  [ TailwindClasses.bg_white
  , TailwindClasses.text_black_20
  , TailwindClasses.border
  , TailwindClasses.rounded
  , TailwindClasses.shadow
  , TailwindClasses.absolute
  , TailwindClasses.z_60
  , TailwindClasses.min_w_50
  ]

dropdownClasses :: Array HH.ClassName
dropdownClasses = menuClasses <>

    [ TailwindClasses.absolute
    , TailwindClasses.top_full
    , TailwindClasses.left_0
    , TailwindClasses.max_h_160
    , TailwindClasses.overflow_y_auto
    ]

droprightClasses :: Array HH.ClassName
droprightClasses = menuClasses <>

    [ TailwindClasses.absolute
    , TailwindClasses.top_0
    , TailwindClasses.left_full
    ]

baseClasses :: Array HH.ClassName
baseClasses =
  [ TailwindClasses.bg_white
  , TailwindClasses.border_gray_80
  , TailwindClasses.border_l_2
  , TailwindClasses.border_r_2
  , TailwindClasses.w_full
  ]

selectionContainerClasses :: Array HH.ClassName
selectionContainerClasses = baseClasses <>

    [ TailwindClasses.border_t_2
    ]

itemContainerClasses :: Array HH.ClassName
itemContainerClasses = baseClasses <>

    [ TailwindClasses.absolute
    , TailwindClasses.shadow
    , TailwindClasses.max_h_120
    , TailwindClasses.overflow_y_auto
    , TailwindClasses.z_50
    , TailwindClasses.border_b_2
    , TailwindClasses.top_full
    , TailwindClasses.left_0
    ]

ulClasses :: Array HH.ClassName
ulClasses =  []

liClasses :: Array HH.ClassName
liClasses =
  [ TailwindClasses.px_4
  , TailwindClasses.py_2
  , TailwindClasses.rounded_sm
  , TailwindClasses.text_black_20
  , TailwindClasses.group
  , HH.ClassName "hover:bg-gray-97"
  , TailwindClasses.cursor_pointer
  ]

selectionGroupClasses :: Array HH.ClassName
selectionGroupClasses =
  [ TailwindClasses.flex
  , TailwindClasses.items_start
  , TailwindClasses.justify_between
  ]

buttonClasses :: Array HH.ClassName
buttonClasses =
  [ TailwindClasses.invisible
  , TailwindClasses.text_gray_80
  , HH.ClassName "hover:text-gray-70"
  , HH.ClassName "group-hover:visible"
  ]

dropdownButton
  :: ∀ p r i
   . (Array (IProp r i) -> Array (HH.HTML p i) -> HH.HTML p i)
  -> Array (IProp r i)
  -> Array (HH.HTML p i)
  -> HH.HTML p i
dropdownButton button iprops html =
  button
    ( [ HP.classes [ TailwindClasses.font_medium, TailwindClasses.flex, TailwindClasses.items_center ] ] <&> iprops )
    $ html <> [ Icon.caratDown [ HP.classes [ TailwindClasses.ml_3, TailwindClasses.text_xs ] ] ]

dropdownContainer
  :: ∀ p action item
   . Array (HH.HTML p (Select.Action action))
  -> (item -> HH.PlainHTML)
  -> (item -> Boolean)
  -> Array item
  -> Maybe Int
  -> HH.HTML p (Select.Action action)
dropdownContainer addlHTML renderItem selected items hix =
  HH.div
    ( Setters.setContainerProps [ HP.classes dropdownClasses ] )
    ( addlHTML <> renderItems )
  where
    renderItems :: Array (HH.HTML p (Select.Action action))
    renderItems =
      [ HH.ul
        [ HP.classes ulClasses ]
        $ mapWithIndex renderItem' items
      ]

    renderItem' :: Int -> item -> HH.HTML p (Select.Action action)
    renderItem' idx item =
      dropdownItem HH.li
        ( Setters.setItemProps idx [] )
        [ HH.fromPlainHTML $ renderItem item ]
        ( selected item )
        ( hix == Just idx )

dropdownItem
  :: ∀ p r i
   . (Array (IProp r i) -> Array (HH.HTML p i) -> HH.HTML p i)
  -> Array (IProp r i)
  -> Array (HH.HTML p i)
  -> Boolean
  -> Boolean
  -> HH.HTML p i
dropdownItem elem props html selected highlighted =
  elem
    ( props <&> [ HP.classes itemClasses ] )
    $ [ Icon.selected [ HP.classes checkmarkClass ] ] <> html
  where
    itemClasses :: Array HH.ClassName
    itemClasses =
      liClasses
      <> [ TailwindClasses.flex ]
      <> ( if highlighted then [ TailwindClasses.bg_gray_lighter ] else [] )
      <> if selected then [ TailwindClasses.font_medium ] else []

    checkmarkClass :: Array HH.ClassName
    checkmarkClass =
      ( [ TailwindClasses.mr_2
        , TailwindClasses.text_green
        ]
      )
      <> if selected then [] else [ TailwindClasses.invisible ]

-- Provided an array of items and any additional HTML, renders the container
-- Items should have already been treated with `boldMatches` by this point.
itemContainer
  :: ∀ p action
   . Maybe Int
  -> Array HH.PlainHTML
  -> Array (HH.HTML p (Select.Action action))
  -> HH.HTML p (Select.Action action)
itemContainer highlightIndex itemsHTML addlHTML =
  HH.div
    ( Setters.setContainerProps [ HP.classes itemContainerClasses ] )
    ( renderItems <> addlHTML )
  where
    hover :: Int -> Array HH.ClassName
    hover i = if highlightIndex == Just i then  [ TailwindClasses.bg_gray_lighter ] else mempty

    renderItems :: Array (HH.HTML p (Select.Action action))
    renderItems =
      [ HH.ul
        [ HP.classes ulClasses ]
        $ mapWithIndex
          ( \i h ->
              HH.li
                ( Setters.setItemProps i
                  [ HP.classes $ liClasses <> hover i ]
                )
                [ HH.fromPlainHTML h ]
          )
          itemsHTML
      ]


-- Provided an array of selection items, renders them in a container
-- Make sure the array of items includes the correct click handlers
selectionContainer :: ∀ p i. Array (HH.HTML p i) -> HH.HTML p i
selectionContainer []   =
  HH.div_ []
selectionContainer html =
  HH.div
  [ HP.classes selectionContainerClasses ]
  [ HH.ul
    [ HP.classes ulClasses ]
    $ html <#>
    ( \h ->
        HH.li
          [ HP.classes (TailwindClasses.py_2 : liClasses) ]
          [ h ]
    )
  ]


selectionGroup
  :: ∀ item i p
   . (item -> HH.PlainHTML)
  -> Array (HH.IProp HTMLdiv p)
  -> Array (HH.IProp HTMLbutton p)
  -> item
  -> HH.HTML i p
selectionGroup f divprops btnprops item =
  HH.div
    ([ HP.classes $ selectionGroupClasses ] <&> divprops)
    [ HH.fromPlainHTML (f item)
    , HH.button
      ([ HP.classes buttonClasses ] <&> btnprops)
      [ HH.text "✕" ]
    ]


-- Takes a key to the segment that you want to display highlighted.
-- WARN: If the key you provided does not exist in the map, your item will not be
-- rendered!
boldMatches :: ∀ item i p. String -> Fuzzy item -> Array (HH.HTML i p)
boldMatches key (Fuzzy { segments }) = boldMatch <$> (fromMaybe [ Left key ] $ lookup key segments)
  where
    boldMatch (Left str) = HH.text str
    boldMatch (Right str) = HH.span [ HP.classes [ TailwindClasses.font_bold ] ] [ HH.text str ]
