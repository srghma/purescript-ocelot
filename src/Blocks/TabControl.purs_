module Ocelot.Block.TabControl where

import Prelude

import TailwindClasses as TailwindClasses
import DOM.HTML.Indexed (HTMLdiv, HTMLa)
import Halogen.HTML as HH
import Halogen.HTML.Properties as HP
import Ocelot.Block.Icon as Icon
import Ocelot.HTML.Properties ((<&>))

type Tab i page =
  { name :: String
  , props :: Array (HH.IProp HTMLa i)
  , page :: page
  , errors :: Int
  }

type TabConfig tabType page =
  { tabs :: Array (tabType page)
  , activePage :: page
  }

type IsActive = Boolean

outerClasses :: Array HH.ClassName
outerClasses =
  [ TailwindClasses.bg_black_10
  , TailwindClasses.w_full
  ]

innerClasses :: Array HH.ClassName
innerClasses =
  [ TailwindClasses.container
  , TailwindClasses.items_end
  , TailwindClasses.mx_auto
  , TailwindClasses.flex
  , TailwindClasses.h_16
  ]

tabClasses :: Array HH.ClassName
tabClasses =
  [ TailwindClasses.pt_5
  , TailwindClasses.pb_6
  , TailwindClasses.inline_flex
  , TailwindClasses.no_underline
  ]

activeTabClasses :: Array HH.ClassName
activeTabClasses =
  [ TailwindClasses.border_b_2
  , TailwindClasses.border_blue_88
  , TailwindClasses.text_white
  ]

inactiveTabClasses :: Array HH.ClassName
inactiveTabClasses =
  [ TailwindClasses.border_b_2
  , TailwindClasses.border_black_10
  , HH.ClassName "hover:border-blue-88"
  , HH.ClassName "hover:text-white"
  , TailwindClasses.text_gray_70
  ]

tabTextClasses :: Array HH.ClassName
tabTextClasses =
  [ TailwindClasses.text_sm
  , TailwindClasses.tracking_wide
  , TailwindClasses.uppercase
  , TailwindClasses.font_bold
  , TailwindClasses.inline_flex
  , TailwindClasses.self_end
  ]

errorIconClasses :: Array HH.ClassName
errorIconClasses =
  [ TailwindClasses.text_2xl
  , TailwindClasses.text_red
  , TailwindClasses.mr_1
  , TailwindClasses.inline_flex
  , TailwindClasses.align_bottom
  , TailwindClasses.my_px
  ]

tabControl
  :: ∀ p i page
   . Eq page
  => TabConfig (Tab i) page
  -> Array (HH.IProp HTMLdiv i)
  -> HH.HTML p i
tabControl { tabs, activePage } props =
  HH.div
    [ HP.classes outerClasses ]
    [ HH.ul
      ( [ HP.classes innerClasses ] <&> props )
      $ singleTab activePage <$> tabs
    ]

tabControl_
  :: ∀ p i page
   . Eq page
  => TabConfig (Tab i) page
  -> HH.HTML p i
tabControl_ config = tabControl config []

singleTab
  :: ∀ p i page
   . Eq page
  => page
  -> Tab i page
  -> HH.HTML p i
singleTab activePage tab =
  HH.li
    [ HP.classes [ TailwindClasses.mr_12 ] ]
    [ HH.a
      ( tab.props
        <> [ HP.classes $ tabClasses <> conditionalTabClasses isActive ]
      )
      ( errorIcon
        <>
        [ HH.span
          [ HP.classes tabTextClasses ]
          [ HH.text tab.name ]
        ]
      )
    ]
  where
    isActive :: Boolean
    isActive = tab.page == activePage

    errorIcon :: Array (HH.HTML p i)
    errorIcon
      | tab.errors > 0 && isActive == false =
        [ Icon.error
          [ HP.classes $ errorIconClasses ]
        ]
      | otherwise = []

    conditionalTabClasses :: IsActive -> Array HH.ClassName
    conditionalTabClasses true = activeTabClasses
    conditionalTabClasses false = inactiveTabClasses
