module Ocelot.Block.Tray where

import Prelude

import TailwindClasses as TailwindClasses
import Data.Array (foldr, snoc)
import Data.Bifunctor (lmap, rmap)
import Data.Tuple (Tuple(..))
import DOM.HTML.Indexed (HTMLdiv)
import Halogen.HTML as HH
import Halogen.HTML.Core as HC
import Halogen.HTML.Properties as HP
import Ocelot.HTML.Properties ((<&>))
import Unsafe.Coerce (unsafeCoerce)

type Tray r = ( open :: Boolean | r )

type HTMLtray = Tray HTMLdiv

open :: ∀ r i. Boolean -> HP.IProp ( open :: Boolean | r ) i
open = HP.prop (HH.PropName "open")

trayClasses :: Array HH.ClassName
trayClasses =
  [ TailwindClasses.fixed
  , TailwindClasses.bg_white
  , TailwindClasses.p_6
  , TailwindClasses.bottom_0
  , TailwindClasses.left_0
  , TailwindClasses.shadow
  , TailwindClasses.border_t
  , TailwindClasses.border_grey_90
  , HH.ClassName "transition-1/4-out"
  , TailwindClasses.w_full
  , TailwindClasses.flex
  , TailwindClasses.items_center
  ]

trayOpenClasses :: Array HH.ClassName
trayOpenClasses =
  [ TailwindClasses.bottom_0
  ]

trayClosedClasses :: Array HH.ClassName
trayClosedClasses =
  [ HH.ClassName "-bottom-40"
  ]

tray
  :: ∀ p i
   . Array (HH.IProp HTMLtray i)
  -> Array (HH.HTML p i)
  -> HH.HTML p i
tray iprops html = HH.div
  ( [ HP.classes (trayClasses <> trayClasses') ] <&> iprops' )
  html
  where
    Tuple open iprops' = pullOpenProp iprops

    trayClasses' =
      if open
        then trayOpenClasses
        else trayClosedClasses

pullOpenProp
  :: ∀ r i
   . Array (HH.IProp ( open :: Boolean | r ) i)
  -> Tuple Boolean (Array (HH.IProp r i))
pullOpenProp = foldr f (Tuple false [])
  where
    f (HP.IProp (HC.Property "open" x)) =
      lmap $ const $ coerceExpanded x
    f iprop = rmap $ (flip snoc) $ coerceR iprop

    coerceExpanded :: HC.PropValue -> Boolean
    coerceExpanded = unsafeCoerce

    coerceR :: HH.IProp ( open :: Boolean | r ) i -> HH.IProp r i
    coerceR = unsafeCoerce
