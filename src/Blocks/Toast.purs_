module Ocelot.Block.Toast where

import Prelude

import TailwindClasses as TailwindClasses
import DOM.HTML.Indexed (HTMLdiv)
import Data.Array (snoc)
import Data.Bifunctor (lmap, rmap)
import Data.Foldable (foldr)
import Data.Tuple (Tuple(..))
import Halogen.HTML as HH
import Halogen.HTML.Core as HC
import Halogen.HTML.Properties as HP
import Ocelot.HTML.Properties ((<&>))
import Unsafe.Coerce (unsafeCoerce)

type Toast r = ( visible :: Boolean | r )

type HTMLtoast = Toast HTMLdiv

visible :: ∀ r i. Boolean -> HP.IProp ( visible :: Boolean | r ) i
visible = HP.prop (HH.PropName "visible")

-- Necessary for centering the toast
toastContainerClasses :: Array HH.ClassName
toastContainerClasses =
  [ TailwindClasses.flex
  , HH.ClassName "transition-1/4-in"
  , HH.ClassName "transition-1/2-out"
  , TailwindClasses.items_center
  , TailwindClasses.fixed
  , TailwindClasses.left_0
  , TailwindClasses.right_0
  , TailwindClasses.bottom_0
  ]

containerVisibleClasses :: Array HH.ClassName
containerVisibleClasses =
  [ TailwindClasses.mb_8
  ]

containerClosedClasses :: Array HH.ClassName
containerClosedClasses =
  [ HH.ClassName "-mb-40"
  ]

toastClasses :: Array HH.ClassName
toastClasses =
  [ TailwindClasses.shadow_md
  , TailwindClasses.p_4
  , TailwindClasses.ml_auto
  , TailwindClasses.mr_auto
  , TailwindClasses.items_center
  , TailwindClasses.border
  , TailwindClasses.border_gray_80
  , TailwindClasses.bg_white
  , TailwindClasses.rounded
  , TailwindClasses.flex
  ]

toast
  :: ∀ p i
   . Array (HH.IProp HTMLtoast i)
  -> Array (HH.HTML p i)
  -> HH.HTML p i
toast iprops html =
  HH.div
  [ HP.classes $ toastContainerClasses <> containerClasses' ]
  [ HH.div
    ( [ HP.classes toastClasses ] <&> iprops' )
    html
  ]
  where
    Tuple visible iprops' = pullVisibleProp iprops
    containerClasses' =
      if visible
        then containerVisibleClasses
        else containerClosedClasses


pullVisibleProp
  :: ∀ r i
   . Array (HH.IProp ( visible :: Boolean | r ) i)
  -> Tuple Boolean (Array (HH.IProp r i))
pullVisibleProp = foldr f (Tuple false [])
  where
    f (HP.IProp (HC.Property "visible" x)) =
      lmap $ const $ coerceExpanded x
    f iprop = rmap $ (flip snoc) $ coerceR iprop

    coerceExpanded :: HC.PropValue -> Boolean
    coerceExpanded = unsafeCoerce

    coerceR :: HH.IProp ( visible :: Boolean | r ) i -> HH.IProp r i
    coerceR = unsafeCoerce
