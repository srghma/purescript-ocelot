-- | Not an entire component, but pieces that only make sense within
-- | the context of components that meet the constraints on these
-- | functions.
module Ocelot.Part.Modal where

import Prelude

import TailwindClasses as TailwindClasses
import DOM.HTML.Indexed (HTMLdiv)
import Data.Maybe (Maybe(..))
import Effect.Aff.Class (class MonadAff)
import Halogen as H
import Halogen.HTML as HH
import Halogen.HTML.Events as HE
import Halogen.HTML.Properties as HP
import Halogen.Query.EventSource as ES
import Ocelot.Block.Format as Format
import Ocelot.HTML.Properties (css, (<&>))
import Web.HTML (window)
import Web.HTML.HTMLDocument as HTMLDocument
import Web.HTML.Window (document)
import Web.UIEvent.KeyboardEvent as KE
import Web.UIEvent.KeyboardEvent.EventTypes as KET

----------
-- Eval Partials

-- A function that will register an event source on the window
initializeWith
  :: ∀ state action slots output m
   . MonadAff m
  => (KE.KeyboardEvent -> Maybe action)
  -> H.HalogenM state action slots output m H.SubscriptionId
initializeWith toAction = do
  document <- H.liftEffect $ document =<< window
  H.subscribe
    $ ES.eventListenerEventSource
      KET.keydown
      (HTMLDocument.toEventTarget document)
      (toAction <=< KE.fromEvent)

whenClose
  :: ∀ state action slots output m
   . MonadAff m
  => KE.KeyboardEvent
  -> H.SubscriptionId
  -> H.HalogenM state action slots output m Unit
  -> H.HalogenM state action slots output m Unit
whenClose ev sid close =
  when (KE.code ev == "Escape") do
    H.unsubscribe sid
    close

----------
-- Render Partials

-- Modals already come with the ClickOutside event baked in, while
-- end users are responsible for handling it somehow.
modal
  :: ∀ action slots m
   . action
  -> Array (HH.IProp HTMLdiv action)
  -> Array (H.ComponentHTML action slots m)
  -> H.ComponentHTML action slots m
modal click iprops html =
  HH.div_
    [ HH.div
        [ HP.classes backgroundClasses
        , HE.onClick $ Just <<< const click
        ]
        []
    , HH.div
        ( [ HP.classes modalClasses ] <&> iprops )
        html
    ]

modal_
  :: ∀ action slots m
   . action
  -> Array (H.ComponentHTML action slots m)
  -> H.ComponentHTML action slots m
modal_ query = modal query []


-----------
-- Blocks

type HeaderProps p i =
  { buttons :: Array (HH.HTML p i)
  , title :: Array (HH.HTML p i)
  }

backgroundClasses :: Array HH.ClassName
backgroundClasses =
  [ TailwindClasses.fixed
  , TailwindClasses.inset_0
  , HH.ClassName "bg-black-modal-a90"
  , TailwindClasses.fade_in
  , TailwindClasses.z_10
  ]

modalClasses :: Array HH.ClassName
modalClasses =
  [ TailwindClasses.fixed
  , TailwindClasses.inset_x_0
  , TailwindClasses.top_0
  , TailwindClasses.my_20
  , TailwindClasses.m_auto
  , TailwindClasses.max_w_lg
  , TailwindClasses.slide_down
  , TailwindClasses.z_10
  ]

bodyClasses :: Array HH.ClassName
bodyClasses =
  [ TailwindClasses.relative
  , TailwindClasses.bg_grey_95
  , TailwindClasses.overflow_auto
  , TailwindClasses.max_h_full
  , TailwindClasses.w_full
  , TailwindClasses.flex_col
  , TailwindClasses.flex
  , TailwindClasses.rounded_b
  ]

body
  :: ∀ p i
   . Array (HH.IProp HTMLdiv i)
  -> Array (HH.HTML p i)
  -> HH.HTML p i
body iprops html =
  HH.div
    ( [ HP.classes bodyClasses ] <&> iprops )
    html

body_
  :: ∀ p i
   . Array (HH.HTML p i)
  -> HH.HTML p i
body_ = body []

headerClasses :: Array HH.ClassName
headerClasses =
  [ TailwindClasses.h_24
  , TailwindClasses.flex
  ]

outerHeaderClasses :: Array HH.ClassName
outerHeaderClasses =
  [ TailwindClasses.bg_white
  , TailwindClasses.w_full
  , TailwindClasses.px_6
  , TailwindClasses.items_center
  , TailwindClasses.flex
  , TailwindClasses.rounded_t
  ]

innerHeaderClasses :: Array HH.ClassName
innerHeaderClasses =
  [ TailwindClasses.w_full
  , TailwindClasses.items_center
  , TailwindClasses.mx_auto
  , TailwindClasses.flex
  ]

header
  :: ∀ p i
   . HeaderProps p i
  -> HH.HTML p i
header props =
  HH.div
    [ HP.classes headerClasses ]
    [ HH.header
      [ HP.classes outerHeaderClasses ]
      ( [ HH.div
        [ HP.classes innerHeaderClasses ]
          [ Format.subHeading
            [ HP.classes [ TailwindClasses.mb_0 ] ]
            props.title
          ]
        ]
        <> props.buttons
      )
    ]
